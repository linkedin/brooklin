allprojects {
  version = "5.1.1-SNAPSHOT"
}

subprojects {
  apply plugin: 'maven-publish'
  apply plugin: 'com.jfrog.bintray'
  apply plugin: 'com.jfrog.artifactory'
  apply plugin: 'pegasus'

  task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
  }

  task dataTemplateSourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.mainGeneratedDataTemplate.allJava
  }

  task testDataTemplateSourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.testGeneratedDataTemplate.allJava
  }

  task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
  }

  task testJar(type: Jar) {
    from sourceSets.test.allJava
    classifier = "tests"
  }

  def allPublications = ['mavenJava', 'dataTemplateJava', 'testDataTemplateJava']

  if (project.tasks.findByName("mainRestClientJar")) {

    task restClientSourceJar(type: Jar) {
      classifier = 'sources'
      from sourceSets.mainGeneratedRest.allJava
    }

    publishing {
      publications {
        restClientJava(MavenPublication) {
          artifactId project.name + "-rest-client"
          artifact mainRestClientJar
          artifact restClientSourceJar
          artifact javadocJar

          version project.version

          pom {
            name = 'Brooklin'
            description = 'An extensible distributed system for reliable nearline data streaming at scale'
            url = 'https://www.github.com/linkedin/brooklin'

            licenses {
              license {
                name = 'Copyright 2019 Linkedin, All Rights Reserved'
                url = 'https://www.github.com/linkedin/brooklin/blob/master/LICENSE'
              }
            }
          }
        }
      }
    }

    allPublications.add('restClientJava')
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java

        artifact sourcesJar
        artifact javadocJar
        artifact testJar

        version project.version

        // we strive to meet https://central.sonatype.org/pages/requirements.html
        pom {
          name = 'Brooklin'
          description = 'An extensible distributed system for reliable nearline data streaming at scale'
          url = 'https://www.github.com/linkedin/brooklin'

          // Default packaging is jar, so only specify otherwise for non-jars
          if (project.pluginManager.hasPlugin('war')) {
            packaging = 'war'
          }

          licenses {
            license {
              name = 'Copyright 2019 Linkedin, All Rights Reserved'
              url = 'https://www.github.com/linkedin/brooklin/blob/master/LICENSE'
            }
          }
          developers {
            developer {
              name = 'Brooklin Development Team'
              email = 'brooklin-oss@linkedin.com'
              organization = 'LinkedIn'
              organizationUrl = 'linkedin.com'
            }
          }
          scm {
            connection = 'scm:git:git://github.com:linkedin/brooklin.git'
            developerConnection = 'scm:git:ssh://github.com:linkedin/brooklin.git'
            url = 'https://www.github.com/linkedin/brooklin'
          }
        }
      }

      dataTemplateJava(MavenPublication) {
        artifactId project.name + "-data-template"
        artifact mainDataTemplateJar
        artifact dataTemplateSourceJar
        artifact javadocJar

        version project.version

        pom {
          name = 'Brooklin'
          description = 'An extensible distributed system for reliable nearline data streaming at scale'
          url = 'https://www.github.com/linkedin/brooklin'

          licenses {
            license {
              name = 'Copyright 2019 Linkedin, All Rights Reserved'
              url = 'https://www.github.com/linkedin/brooklin/blob/master/LICENSE'
            }
          }
        }
      }

      testDataTemplateJava(MavenPublication) {
        artifactId project.name + "-test-data-template"
        artifact testDataTemplateJar
        artifact testDataTemplateSourceJar
        artifact javadocJar

        version project.version

        pom {
          name = 'Brooklin'
          description = 'An extensible distributed system for reliable nearline data streaming at scale'
          url = 'https://www.github.com/linkedin/brooklin'

          licenses {
            license {
              name = 'Copyright 2019 Linkedin, All Rights Reserved'
              url = 'https://www.github.com/linkedin/brooklin/blob/master/LICENSE'
            }
          }
        }
      }
    }
  }

  // Artifactory publishing
  artifactory {
    //docs: https://www.jfrog.com/confluence/display/rtf/gradle+artifactory+plugin
    contextUrl = 'https://linkedin.jfrog.io/artifactory'
    publish {
      repository {
        repoKey = 'brooklin'
        username = System.getenv('ARTIFACTORY_USER')
        password = System.getenv('ARTIFACTORY_API_KEY')
        maven = true
      }

      defaults {
        allPublications.each {
          publication -> publications(publication)
        }
      }
    }
  }

  artifactoryPublish {
    skip = project.hasProperty('artifactory.dryRun')

    doFirst {
      println "Publishing $jar.baseName to Artifactory (dryRun: $skip), publish: $publish)"
    }
  }

  bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = allPublications
    pkg {
      repo = 'maven'
      name = 'brooklin'
      userOrg = 'linkedin'
      licenses = ['BSD 2-Clause']
      vcsUrl = 'https://www.github.com/linkedin/brooklin'
      publicDownloadNumbers = true
      version {
        name = project.version
      }
      publish = false
      override = false
    }
  }
}
