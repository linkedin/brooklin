apply from: file('gradle/license.gradle')
apply from: file('gradle/environment.gradle')
apply from: file("gradle/dependency-versions.gradle")
apply from: file("gradle/install-git-hooks.gradle")

buildscript {
  repositories {
    mavenCentral()
  }
  apply from: file('gradle/buildscript.gradle'), to: buildscript
}

allprojects {
  group = "com.github.datastream"

  apply plugin: 'eclipse'
  apply plugin: 'idea'
  apply plugin: 'project-report'
  apply plugin: 'checkstyle'
  apply plugin: 'findbugs'

  repositories {
    mavenCentral()
    maven {
      url  "https://linkedin.bintray.com/maven"
    }
    mavenLocal()
  }
}

idea {
  project {
    languageLevel = 1.8
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'pegasus'

  testReportDirName = "${project.rootDir}/out/${project.name}/"

  // Avoid failure in internal LinkedIn build validation
  if (!ext.has('spec')) {
    ext.spec = [:]
  }

  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'

  checkstyle {
    configFile = file("${project.rootDir}/checkstyle/checkstyle.xml")
    sourceSets = [ getProject().sourceSets.main, getProject().sourceSets.test ]
    toolVersion = "6.7"
    ignoreFailures = false
  }

  findbugs {
    toolVersion = "3.0.1"
    sourceSets = [sourceSets.main] // Test code is not included
    reportsDir = file("$project.buildDir/findbugsReports")
    reportLevel = "medium"
    effort = "default"
    excludeFilter = file("$rootProject.projectDir/findbugs/excludeFilter.xml")
  }

  tasks.withType(FindBugs) {
    reports {
      xml.enabled false
      html.enabled true
    }
  }

  spec['product'] = ['pegasus':
                         ['data'        : 'com.linkedin.pegasus:data:' + pegasusVersion,
                          'restliCommon': 'com.linkedin.pegasus:restli-common:' + pegasusVersion,
                          'restliClient': 'com.linkedin.pegasus:restli-client:' + pegasusVersion,]
  ]

  // comment this out so the java files don't get auto-generated every time
  afterEvaluate {
    if (project.plugins.hasPlugin('pegasus')) {
      // add the standard pegasus dependencies wherever the plugin is used
      dependencies {
        dataTemplateCompile spec.product.pegasus.data
        restClientCompile spec.product.pegasus.restliClient

        // needed for Gradle 1.9+
        restClientCompile spec.product.pegasus.restliCommon
      }
    }
  }

  def results = [:].withDefault { [] }
  test {
    useTestNG()
    maxParallelForks = 1
    maxHeapSize = "2048m"
    testLogging {
      exceptionFormat = 'full'
      events "started", "passed", "skipped", "failed"
    }
    onOutput { descriptor, event ->
      results[descriptor.toString()] << event.message
    }

    afterTest { descriptor, result ->
      if(result.getResultType() == TestResult.ResultType.FAILURE) {
        logger.lifecycle("${descriptor.toString()} failed with output:\n${results[descriptor.toString()].join('')}")
      }
      results[descriptor.toString()] = []
    }
  }

  dependencies {
    compile "log4j:log4j:$log4jVersion"
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "org.slf4j:slf4j-log4j12:$slf4jVersion"
    compile "org.apache.commons:commons-lang3:$commonslang3Version"
    compile "io.dropwizard.metrics:metrics-core:$metricsCoreVersion"
    compile "io.dropwizard.metrics:metrics-jmx:$metricsCoreVersion"
    testCompile "org.testng:testng:$testngVersion"
  }

  jar {
    manifest {
      attributes 'Implementation-Title': 'Datastream'
    }
  }

  tasks.withType(JavaCompile) {
    // Skipping 'deprecation' since pegasus generates problematic files and 'fallthrough' since it can't be suppressed
    // Xlint:all - [deprecation, fallthrough]
    options.compilerArgs = ["-Xlint:cast,classfile,dep-ann,divzero,empty,finally,options,overrides,path,processing,rawtypes,serial,static,try,unchecked,varargs", "-Werror"]
  }
}

project(':datastream-common') {
  dependencies {
    compile "org.apache.avro:avro:$avroVersion"
    compile "com.linkedin.pegasus:restli-server:$pegasusVersion"
    compile "com.intellij:annotations:$intellijAnnotationsVersion"
    compile "com.google.guava:guava:$guavaVersion"
    testCompile "org.mockito:mockito-core:$mockitoVersion"
  }
}

project(':datastream-server-api') {
  dependencies {
    compile project(':datastream-common')
    compile project(':datastream-utils')
    compile "com.linkedin.kafka.clients:li-apache-kafka-clients:$LIKafkaVersion"
  }
}

project(':datastream-utils') {
  dependencies {
    compile project(':datastream-common')
    compile "com.101tec:zkclient:$zkclientVersion"
    compile "com.google.guava:guava:$guavaVersion"
    testCompile project(':datastream-kafka')
    testCompile project(':datastream-testcommon')
  }
}

project(':datastream-file-connector') {
  dependencies {
    compile project(':datastream-server-api')
    compile project(':datastream-common')
    compile project(':datastream-utils')
    testCompile project(':datastream-server')
    testCompile project(':datastream-testcommon')
    testCompile "org.mockito:mockito-core:$mockitoVersion"
  }
}

project(':datastream-directory') {
  dependencies {
    compile project(':datastream-server-api')
    compile project(':datastream-common')
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "org.apache.commons:commons-lang3:$commonslang3Version"
    compile "commons-io:commons-io:$commonsIOVersion"
    compile "com.google.guava:guava:$guavaVersion"
  }
}

project(':datastream-kafka') {
  dependencies {
    compile "com.linkedin.kafka:kafka_$scalaVersion:$kafkaVersion"
    compile "com.linkedin.kafka.clients:li-apache-kafka-clients:$LIKafkaVersion"
    compile "commons-httpclient:commons-httpclient:$commonsHttpClientVersion"

    compile project(':datastream-server')
    compile project(':datastream-server-api')
    compile project(':datastream-utils')

    testCompile project(':datastream-testcommon')
    testCompile "org.mockito:mockito-core:$mockitoVersion"

    tasks.create(name: "copyDependentLibs", type: Copy) {
      from (configurations.runtime) {
      }
      into "$buildDir/dependent-libs"
    }

    jar {
      dependsOn 'copyDependentLibs'
    }
  }
}

project(':datastream-kafka-factory-impl') {
  dependencies {
    compile project(':datastream-kafka-connector')
    compile "com.linkedin.kafka.clients:li-apache-kafka-clients:$LIKafkaVersion"

    testCompile project(':datastream-kafka')
  }
}

project(':datastream-kafka-connector') {
  dependencies {
    compile project(':datastream-server-api')
    compile project(':datastream-kafka')
    compile "com.linkedin.kafka.clients:li-apache-kafka-clients:$LIKafkaVersion"
    compile "org.apache.httpcomponents:httpclient:$apacheHttpClientVersion"
    compile "commons-validator:commons-validator:$commonsValidatorVersion"

    testCompile project(':datastream-kafka')
    testCompile project(':datastream-testcommon')
    testCompile project(':datastream-kafka-factory-impl')
    testCompile "org.mockito:mockito-core:$mockitoVersion"
  }
}

project(':datastream-testcommon') {
  dependencies {
    compile project(':datastream-server-api')
    compile project(':datastream-kafka')
    compile project(':datastream-common')
    compile project(':datastream-utils')
    compile project(':datastream-server')
    compile project(':datastream-server-restli')
    compile project(':datastream-client')
    compile "com.intellij:annotations:$intellijAnnotationsVersion"
    compile "commons-cli:commons-cli:$commonsCliVersion"
    compile "org.apache.avro:avro:$avroVersion"
    compile "org.apache.zookeeper:zookeeper:$zookeeperVersion"
    compile "com.linkedin.pegasus:restli-server:$pegasusVersion"
    compile "org.testng:testng:$testngVersion"
    compile "com.google.guava:guava:$guavaVersion"
  }
}

project(':datastream-tools') {
  dependencies {
    compile project(':datastream-common')
    compile project(':datastream-client')
    compile project(':datastream-server')
    compile project(':datastream-testcommon')
    compile "commons-cli:commons-cli:$commonsCliVersion"
  }

  tasks.create(name: "releaseTarGz", dependsOn: configurations.archives.artifacts, type: Tar) {
    description = "Create Brooklin release tarball"
    baseName = "${rootProject.name}"
    into { "${property('baseName')}-$rootProject.version" }
    compression = Compression.GZIP
    from(rootProject.file("NOTICE"))
    from(rootProject.file("LICENSE"))
    from(rootProject.file("scripts")) { exclude "git" into "bin/" }
    from(rootProject.file("config")) { into "config/" }
    from(configurations.runtime) { into("libs/") }
    from(configurations.archives.artifacts.files) { into("libs/") }
    from(project(':datastream-tools').jar) { into("libs/") }
    from(project(':datastream-tools').configurations.runtime) { into("libs/") }
    from(project(':datastream-server').jar) { into("libs/") }
    from(project(':datastream-server').configurations.runtime) { into("libs/") }
    from(project(':datastream-file-connector').jar) { into("libs/") }
    from(project(':datastream-file-connector').configurations.runtime) { into("libs/") }
    from(project(':datastream-directory').jar) { into("libs/") }
    from(project(':datastream-directory').configurations.runtime) { into("libs/") }
    from(project(':datastream-testcommon').jar) { into("libs/") }
    from(project(':datastream-testcommon').configurations.runtime) { into("libs/") }
    from(project(':datastream-common').jar) { into("libs/") }
    from(project(':datastream-common').configurations.runtime) { into("libs/") }
    from(project(':datastream-kafka').jar) { into("libs/") }
    from(project(':datastream-kafka').configurations.runtime) { into("libs/") }
    from(project(':datastream-kafka-connector').jar) { into("libs/") }
    from(project(':datastream-kafka-connector').configurations.runtime) { into("libs/") }
    duplicatesStrategy 'exclude'
  }
}

project(':datastream-client') {

  dependencies {
    compile project(path: ':datastream-common', configuration: 'restClient')

    compile "com.linkedin.pegasus:restli-client:$pegasusVersion"
    compile "com.linkedin.pegasus:r2-netty:$pegasusVersion"
    compile "org.apache.avro:avro:$avroVersion"

    compile project(':datastream-common')
    compile project(':datastream-utils')

    testCompile project(':datastream-testcommon')
    testCompile project(':datastream-kafka')
    testCompile project(':datastream-server')
    testCompile "org.mockito:mockito-core:$mockitoVersion"
  }
}

project(':datastream-server') {

  dependencies {
    compile "com.101tec:zkclient:$zkclientVersion"
    compile "org.codehaus.jackson:jackson-core-asl:$jacksonVersion"
    compile "org.codehaus.jackson:jackson-mapper-asl:$jacksonVersion"

    compile project(':datastream-server-api')
    compile project(':datastream-common')
    compile project(':datastream-utils')
    compile project(':datastream-client')

    testCompile project(':datastream-kafka')
    testCompile "com.linkedin.kafka:kafka_$scalaVersion:$kafkaVersion"
    testCompile project(':datastream-client')
    testCompile project(':datastream-testcommon')
    testCompile "org.mockito:mockito-core:$mockitoVersion"
  }

  tasks.create(name: "copyDependentLibs", type: Copy) {
    from (configurations.runtime) {
    }
    into "$buildDir/dependent-libs"
  }

  jar {
    dependsOn 'copyDependentLibs'
  }
}

project(':datastream-server-restli') {
  ext.apiProject = project(':datastream-common')
  pegasus.main.idlOptions.addIdlItem(['com.linkedin.datastream.server.dms', 'com.linkedin.datastream.server.diagnostics'])

  dependencies {
    compile "com.linkedin.pegasus:restli-server:$pegasusVersion"
    compile "com.linkedin.pegasus:restli-netty-standalone:$pegasusVersion"
    compile "com.linkedin.pegasus:r2-jetty:$pegasusVersion"
    compile "com.101tec:zkclient:$zkclientVersion"
    compile "org.codehaus.jackson:jackson-core-asl:$jacksonVersion"
    compile "org.codehaus.jackson:jackson-mapper-asl:$jacksonVersion"
    compile "org.apache.commons:commons-lang3:$commonslang3Version"

    compile project(':datastream-server')
    compile project(':datastream-common')

    testCompile project(':datastream-kafka')
    testCompile "com.linkedin.kafka:kafka_$scalaVersion:$kafkaVersion"
    testCompile project(':datastream-client')
    testCompile project(':datastream-file-connector')
    testCompile project(':datastream-testcommon')
    testCompile "org.mockito:mockito-core:$mockitoVersion"
  }

  tasks.create(name: "copyDependentLibs", type: Copy) {
    from (configurations.runtime) {
    }
    into "$buildDir/dependent-libs"
  }

  jar {
    dependsOn 'copyDependentLibs'
  }

  // For test or experiment purpose
  task startStandaloneRestServer(type: JavaExec) {
    main = 'com.linkedin.restli.server.NettyStandaloneLauncher'
    args = ['-port', '8080', '-packages', 'com.linkedin.datastream.server.dms']
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
  }
}
